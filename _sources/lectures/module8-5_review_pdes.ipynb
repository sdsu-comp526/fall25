{
 "cells": [
  {
   "attachments": {},
   "cell_type": "markdown",
   "id": "b87765e8-3137-4c80-bb38-9daada6c54bc",
   "metadata": {},
   "source": [
    "# 40) Review of PDEs and practice exercises\n",
    "\n",
    "We will implement a PDE simulation of the [heat equation](https://en.wikipedia.org/wiki/Heat_equation).\n",
    "At various points in this exercise, you will have the ability to modify the code to explore different variations on the project.\n",
    "\n",
    "## The math\n",
    "\n",
    "We will consider a perfectly insulated rod of metal with various conditions along each end.\n",
    "We will only consider the temperature change in 1D for this exercise.\n",
    "\n",
    "The partial differential equation which governs the movement of the heat through this rod is given by\n",
    "\n",
    "$$ \\frac{\\partial u}{\\partial t} = \\frac{\\partial^2 u}{\\partial x^2} $$\n",
    "\n",
    "where $u \\left( x, t \\right)$ is the function giving the temperature of the rod at location $x$ and time $t$.\n",
    "\n",
    "Note that $\\Delta u$ or $\\nabla^2 u$ is sometimes used in place of $\\partial^2 u / \\partial x^2$.\n",
    "\n",
    "For this to work, we also need an initial condition $u_0 \\left( x \\right) = u \\left( x, 0 \\right)$ and boundary conditions.\n",
    "For simplicity (and [without loss of generality](https://en.wikipedia.org/wiki/Without_loss_of_generality)), we assume $x \\in \\left[ 0, 1 \\right]$.\n",
    "That means we have boundary conditions $u_L \\left( t \\right) = u \\left( 0, t \\right)$ or $u_L \\left( t \\right) = \\partial u / \\partial t \\left( 0, t \\right)$ and $u_R \\left( t \\right) = u \\left( 1, t \\right)$ or $u_R \\left( t \\right) = \\partial u / \\partial t \\left( 1, t \\right)$.\n",
    "\n",
    "### Extension\n",
    "\n",
    "For simplicity, we consider boundary conditions that do not vary with time, but this is an area for exploration."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8f83a408-a789-4f2d-87b9-4aa59bf84ff0",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "default(linewidth=4, legendfontsize=12)\n",
    "\n",
    "# Our points x\n",
    "n   = 101\n",
    "h_x = 1 / (n - 1)\n",
    "x   = LinRange(0, 1, n)\n",
    "\n",
    "# An initial condition\n",
    "u_0 = sin.(x * Ï€)\n",
    "u_0[end] = 0 # just cleaning up here\n",
    "@show maximum(u_0)\n",
    "\n",
    "# And let's see it\n",
    "plot(LinRange(0, 1, n), u_0, xlabel=\"position\", ylabel=\"temperature\", label=\"\\$u\\$\", title=\"Initial condition\", color=:red)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "290a1e11-9a89-4a6e-b66a-8ad2eb18d5eb",
   "metadata": {},
   "source": [
    "## New wrinkle\n",
    "\n",
    "We have handled temporal derivatives before with time steppers,and as the examples in class, now we add spatial derivatives.\n",
    "\n",
    "We will use the [finite difference method](https://en.wikipedia.org/wiki/Finite_difference_method) here.\n",
    "\n",
    "We have seen the finite difference approximations of derivatives before.\n",
    "For this exercise, we will need a forward and backward difference for the first derivative and a centered difference for the second derivative.\n",
    "\n",
    "$$ \\begin{split}\n",
    "  f'  \\left( x \\right) &\\approx \\frac{f \\left( x + h \\right) - f \\left( x \\right)}{h} \\\\\n",
    "  f'  \\left( x \\right) &\\approx \\frac{f \\left( x \\right) - f \\left( x + h \\right)}{h} \\\\\n",
    "  f'' \\left( x \\right) &\\approx \\frac{f \\left( x - h \\right) - 2 f \\left( x \\right) + f \\left( x + h \\right)}{h^2}\n",
    "\\end{split} $$\n",
    "\n",
    "In the finite difference method, we pick a finite number of evenly spaced points $x_0, x_1, \\dots, x_n$ to simulate our function at.\n",
    "\n",
    "![image.png](../img/FD_function.png)\n",
    "\n",
    "We then make a matrix from the system of equations generated from the this finite difference approximation of the second derivative with respect to $x$\n",
    "\n",
    "$$ u'' \\left( x_n \\right) \\approx \\frac{u \\left( x_{n - 1} \\right) - 2 u \\left( x_n \\right) + u \\left( x_{n + 1} \\right)}{h^2} $$\n",
    "\n",
    "Note that this equation can only be used on the points $x_1, x_2, \\dots, x_{n - 1}$, as we need points to both the right and left of the target point to compute the derivative.\n",
    "This is where our boundary conditions come into play."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6ffb42b1-72e3-40a4-b0b1-146eba40a810",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Let's make a matrix representing this system\n",
    "A = zeros(n, n)\n",
    "\n",
    "for i in 2:n-1\n",
    "    A[i, i-1:i+1] = [1 -2 1] ./ h_x^2\n",
    "end\n",
    "\n",
    "println(\"Matrix A without boundary conditions:\")\n",
    "display(A)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "802549a1-7ace-40ca-9ecb-87154e1fd715",
   "metadata": {},
   "source": [
    "## Boundary conditions\n",
    "\n",
    "We will start with simple boundary conditions, $u_L = u_R = 0$.\n",
    "\n",
    "This means that $u \\left( x_0 \\right)$ and $u \\left( x_n \\right)$ never change, so we will encode this by putting $1$ in the entries for those points."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2db917ea-63ee-4a69-9265-14ec314ba14d",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Boundary conditions\n",
    "A[1, 1] = 1\n",
    "A[n, n] = 1\n",
    "\n",
    "println(\"Matrix A with fixed boundary conditions:\")\n",
    "display(A)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "46fa6c9c-937f-4476-a50d-a18fabca2e6b",
   "metadata": {},
   "source": [
    "## Time Stepper\n",
    "\n",
    "Now we need to pick one of our for advancing our solution forward in time.\n",
    "This is the area where we can experiment the most.\n",
    "For now, let's use a simple [forward Euler](https://en.wikipedia.org/wiki/Euler_method).\n",
    "\n",
    "$$ x_{n + 1} = x_n + h \\, dF \\left( t_n, x_n \\right) $$\n",
    "\n",
    "where $t_n$ is the current time, $x_n$ is the current solution, $h$ is our step size in time, $dF$ is our differential equation, and $x_{n + 1}$ is our new solution.\n",
    "\n",
    "Let's code up forward Euler and take a single step."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8f13f041-eb52-4db7-b2d2-541c48c2525f",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Forward euler\n",
    "function euler_forward(x, h, dF)\n",
    "    return x + h * dF(x)\n",
    "end\n",
    "\n",
    "# And take a single step\n",
    "h  = 1e-6\n",
    "dF(x) = A * x\n",
    "u_1 = euler_forward(u_0, h, dF)\n",
    "@show maximum(u_1)\n",
    "\n",
    "# And plot\n",
    "plot(LinRange(0, 1, n), u_1, xlabel=\"position\", ylabel=\"temperature\", label=\"\\$u\\$\", title=\"Single step\", color=:red)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "089a4598-cf31-4662-b74a-f029312366b4",
   "metadata": {},
   "source": [
    "### Exploration\n",
    "\n",
    "Implement backward Euler for this system instead.\n",
    "And maybe the $\\theta$ method."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "342abc5b-9bae-4215-b3df-d9cbd67f6238",
   "metadata": {},
   "source": [
    "## Building it out\n",
    "\n",
    "Ok, now it seems like we need two things right now.\n",
    "\n",
    "1) Something to take our time stepper (`euler_forward`) and our finite different matrix (`A`) and create a series of outputs.\n",
    "\n",
    "2) Something to automate and animate the plotting process.\n",
    "\n",
    "Let's start with 1).\n",
    "We'll make a basic function `run_simulation` to produce our values $u_n$ via our PDE and time stepper."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "33149a77-4311-4a03-aba6-cfc85ac655de",
   "metadata": {},
   "outputs": [],
   "source": [
    "function run_simulation(u_0, h, dF, time_stepper, t_f)\n",
    "    # Setup\n",
    "    num_steps = Int(ceil(t_f / h))\n",
    "    u = zeros((num_steps + 1, maximum(size(u_0))))\n",
    "    u[1, :] = u_0\n",
    "\n",
    "    # Simulate\n",
    "    for i in 1:num_steps\n",
    "        u[i + 1, :] = time_stepper(u[i, :], h, dF)\n",
    "    end\n",
    "    u\n",
    "end\n",
    "\n",
    "# And run it\n",
    "h   = 1e-5\n",
    "t_f = 1 # s\n",
    "u   = run_simulation(u_0, h, dF, euler_forward, t_f)\n",
    "@show maximum(u[end, :])\n",
    "\n",
    "# Plotting the final u as a sanity check\n",
    "plot(LinRange(0, 1, n), u[end, :], xlims=[0, 1], ylims=[0, 1], xlabel=\"position\", ylabel=\"temperature\", label=\"\\$u\\$\", title=\"Final step\", color=:red)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c4a803db-3895-4a6a-a8c4-2fc905335ebb",
   "metadata": {},
   "source": [
    "It looks like the temperature went to $0$ on the whole rod, which I would expect given the boundary conditions.\n",
    "\n",
    "## Animation\n",
    "\n",
    "Lets make an animation now!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "312d2054-6871-481f-a4a7-26d7ef0920ce",
   "metadata": {},
   "outputs": [],
   "source": [
    "# We wrap the plotting in an annotated loop\n",
    "animation = @animate for i in 1:Int(ceil(t_f / h))\n",
    "    plot(LinRange(0, 1, n), u[i, :], xlims=[0, 1], ylims=[0, 1],\n",
    "        xlabel=\"position\", ylabel=\"temperature\", label=\"\\$u\\$\",\n",
    "        color=:red)\n",
    "end every 100;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ec0e6934-9050-42a6-9f2b-aa7058556672",
   "metadata": {},
   "outputs": [],
   "source": [
    "gif(animation, \"heat_fixed.gif\", fps=30)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "418c7304-b1d5-4ebe-9f4f-5eaa41bbab1d",
   "metadata": {},
   "source": [
    "## Other boundary conditions\n",
    "\n",
    "This looks great, but boring.\n",
    "What happens if we want to insulate the ends of the rod?\n",
    "What does our intuition tell us should be the outcome?\n",
    "\n",
    "Insulating the end of the rods would be equivalent to stating $\\partial u / \\partial t \\left( 0, t \\right) = \\partial u / \\partial t \\left( 1, t \\right) = 0$.\n",
    "We can encode this with finite difference approximations of the first derivative."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "423e88c2-1ea2-4300-845f-9f455112ce66",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Update boundary conditions\n",
    "A[1, 1:2]         = [-1  1] ./ h\n",
    "A[end, end-1:end] = [ 1 -1] ./ h\n",
    "\n",
    "println(\"Matrix A with insulated boundary conditions:\")\n",
    "display(A)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2cf5df59-0976-465d-9ebd-2106d4b421ae",
   "metadata": {},
   "source": [
    "And let's try the simulation again!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "07259ff0-7e8e-4ffb-8537-6d2b6207757b",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Statistics\n",
    "\n",
    "# And run it\n",
    "h   = 1e-5\n",
    "t_f = 1 # s\n",
    "u   = run_simulation(u_0, h, dF, euler_forward, t_f)\n",
    "@show maximum(u[end, :])\n",
    "\n",
    "# Plotting the final u as a sanity check\n",
    "scatter(LinRange(0, 1, n), u[end, :], xlims=[0, 1], ylims=[0, 1], xlabel=\"position\", ylabel=\"temperature\", label=\"\\$u\\$\", title=\"Final step\", color=:red)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ff437577-3c85-44fa-933f-57566d321025",
   "metadata": {},
   "outputs": [],
   "source": [
    "# We wrap the plotting in an annotated loop\n",
    "animation = @animate for i in 1:Int(ceil(t_f / h))\n",
    "    plot(LinRange(0, 1, n), u[i, :], xlims=[0, 1], ylims=[0, 1],\n",
    "        xlabel=\"position\", ylabel=\"temperature\", label=\"\\$u\\$\",\n",
    "        color=:red)\n",
    "end every 100;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1ec29ef7-1bec-4f90-a16e-0184f2029372",
   "metadata": {},
   "outputs": [],
   "source": [
    "gif(animation, \"heat_insulated.gif\", fps=30)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "acbb0400-6f38-4f9f-8825-22a73b7d654c",
   "metadata": {},
   "source": [
    "### Exploration\n",
    "\n",
    "Another popular boundary condition is periodic.\n",
    "In this case, the left and right ends of the rod are joined.\n",
    "Can you implement this?"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
