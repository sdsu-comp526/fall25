{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "d407b543-41ed-4dbe-a3d6-f874932811e9",
   "metadata": {},
   "source": [
    "# 39) Review of ODEs and practice exercises\n",
    "\n",
    "We will implement a ODE simulation of a [simple gravity pendulum](https://en.wikipedia.org/wiki/Pendulum_(mechanics)#Simple_gravity_pendulum).\n",
    "At various points in this exercise, you will have the ability to modify the code to explore different variations on the project.\n",
    "\n",
    "## The math\n",
    "\n",
    "Let us consider a weight at the end of a massless arm attached to a frictionless pivot.\n",
    "We will only consider motion in two dimensions for this example.\n",
    "\n",
    "The differential equation which governs the motion of this pendulum is given by\n",
    "\n",
    "$$ \\frac{d^2 \\theta}{d t^2} + \\frac{g}{l} \\sin \\left( \\theta \\right) = 0 $$\n",
    "\n",
    "where $g$ is the [gravitational constant](https://en.wikipedia.org/wiki/Gravity_of_Earth), $l$ is the length of the arm, and $\\theta$ is the angle from vertical for the pendulum.\n",
    "\n",
    "Note: Wikipedia has a nice pair of derivations for this equation.\n",
    "It is a good example of deriving the equation for our simulation from [first principles](https://en.wikipedia.org/wiki/First_principle) (in this case, physical laws around force, torque, or energy).\n",
    "\n",
    "### Extension\n",
    "\n",
    "Note that we can change the constants $g$ and $l$ here to modify our simulation.\n",
    "We could also change our differential equation to represent more complex behavior.\n",
    "\n",
    "### A complication\n",
    "\n",
    "The time steppers for ODEs we saw in class only support first derivatives with respect to time, not second derivatives.\n",
    "There is a common trick we can employ though.\n",
    "\n",
    "Let $v = \\frac{d \\theta}{d t}$ and note that $\\frac{d v}{d t} = \\frac{d^2 \\theta}{d t^2}$.\n",
    "Then\n",
    "\n",
    "$$ \\begin{split}\n",
    "  \\frac{d \\theta}{d t} &= v\\\\\n",
    "  \\frac{d v}{d t}      &= - \\frac{g}{l} \\sin \\left( \\theta \\right)\n",
    "\\end{split} $$\n",
    "\n",
    "We now have a system of first order equations and can use our techniques from class."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4bbabc62-0948-4250-96fc-ea06e24a7d0f",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Some simulation constants\n",
    "# Note that we can change these\n",
    "g = 9.81 # m/s^2\n",
    "l = 10   # m\n",
    "\n",
    "# Function to represent our physics\n",
    "function dFdt(x)\n",
    "    θ = x[1]\n",
    "    v = x[2]\n",
    "    return [v; (-g/l)*sin(θ)]\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "07c9f497-dfde-4ec7-a287-3bc4578085f6",
   "metadata": {},
   "source": [
    "## Initial conditions\n",
    "\n",
    "We needed initial conditions for our timesteppers in class.\n",
    "Here, we will need initial values for $\\theta$ and $v$, so an initial displacement and initial velocity."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2653b672-8aea-483e-880c-d797a517604b",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Initial conditions\n",
    "θ_0 = π/4 # 180°\n",
    "v_0 = -1  # rad/s\n",
    "x_0 = [θ_0; v_0]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "092d334f-758c-4bf7-bc33-47ee1fe09841",
   "metadata": {},
   "source": [
    "## Visualize\n",
    "\n",
    "It would be really nice if we can visualize out results.\n",
    "We could plot $\\theta$ as a function of $t$, but that wouldn't make a nice graphic of a swinging pendulum.\n",
    "To make a graphic of a swinging pendulum, we need the coordinates $\\left( x, y \\right)$ of our pendulum.\n",
    "\n",
    "We should pick a coordinate system first.\n",
    "Then we would indicate where $\\theta$ is relative to our coordinate system and derive the coordinates of the weight from this diagram.\n",
    "\n",
    "![image.png](../img/pendulum.png)\n",
    "\n",
    "Given this diagram, we can now say that\n",
    "\n",
    "$$ x = l \\sin \\left( \\theta \\right), y = - l \\cos \\left( \\theta \\right) $$\n",
    "\n",
    "With this, we can plot the initial location of our pendulum."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e5bf8843-16f0-4df9-a9b8-5c0613c337b8",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "default(linewidth=4, legendfontsize=12)\n",
    "\n",
    "x =  l * sin(θ_0)\n",
    "y = -l * cos(θ_0)\n",
    "plot([(0, 0), (x, y)],\n",
    "    xlims=[-l, l], ylims=[-l, l],\n",
    "    marker=:circle,\n",
    "    legend=:none, aspect_ratio=:equal)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6ed92699-b104-402a-a625-6363168c012f",
   "metadata": {},
   "source": [
    "## Time Stepper\n",
    "\n",
    "Now we need to pick one of our for advancing our solution forward in time.\n",
    "This is the area where we can experiment the most.\n",
    "For now, let's use a simple [forward Euler](https://en.wikipedia.org/wiki/Euler_method).\n",
    "\n",
    "$$ x_{n + 1} = x_n + h \\, dF \\left( t_n, x_n \\right) $$\n",
    "\n",
    "where $t_n$ is the current time, $x_n$ is the current solution, $h$ is our step size in time, $dF$ is our differential equation, and $x_{n + 1}$ is our new solution.\n",
    "\n",
    "Let's code up forward Euler and take a single step."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2bcf2733-ab78-48cc-953a-e43ca97d0842",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Forward euler\n",
    "function euler_forward(x, h, dF)\n",
    "    return x + h * dF(x)\n",
    "end\n",
    "\n",
    "# And take a single step\n",
    "h  = 1e-1\n",
    "@show x_1 = euler_forward(x_0, h, dFdt)\n",
    "\n",
    "# And plot\n",
    "x =  l * sin(θ_0)\n",
    "y = -l * cos(θ_0)\n",
    "plot([(0, 0), (x, y)],\n",
    "    xlims=[-l, l], ylims=[-l, l],\n",
    "    marker=:circle,\n",
    "    legend=:none, aspect_ratio=:equal)\n",
    "\n",
    "θ_1 = x_1[1]\n",
    "x =  l * sin(θ_1)\n",
    "y = -l * cos(θ_1)\n",
    "plot!([(0, 0), (x, y)], marker=:circle)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5bf4a7cd-24ef-4b1e-ab3f-ed019d7918f6",
   "metadata": {},
   "source": [
    "We can also do this with backward Euler."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a017d0f0-3c07-4c95-bcb1-1fc78067af7b",
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra\n",
    "\n",
    "# Backward euler\n",
    "function euler_backward(x, h, dF)\n",
    "    # Fixed point iteration\n",
    "    x_n1 = x + h * dF(x)\n",
    "    x_n2 = x + h * dF(x_n1)\n",
    "    # Note - this is a simple fixed point iteration - could we do this better?\n",
    "    tol = 1e-10\n",
    "    while (norm(x_n1 - x_n2) > tol)\n",
    "        x_n1 = x_n2\n",
    "        x_n2 = x + h * dF(x_n1)\n",
    "    end\n",
    "    return x_n1\n",
    "end\n",
    "\n",
    "# And take a single step\n",
    "h  = 1e-1\n",
    "@show x_1 = euler_backward(x_0, h, dFdt)\n",
    "\n",
    "# And plot\n",
    "x =  l * sin(θ_0)\n",
    "y = -l * cos(θ_0)\n",
    "plot([(0, 0), (x, y)],\n",
    "    xlims=[-l, l], ylims=[-l, l],\n",
    "    marker=:circle,\n",
    "    legend=:none, aspect_ratio=:equal)\n",
    "\n",
    "θ_1 = x_1[1]\n",
    "x =  l * sin(θ_1)\n",
    "y = -l * cos(θ_1)\n",
    "plot!([(0, 0), (x, y)], marker=:circle)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b60e5419-4a24-4c29-8be3-8bc7d92d599d",
   "metadata": {},
   "source": [
    "### Exploration\n",
    "\n",
    "Implement the $\\theta$ method."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a36c80e4-10c2-4b68-b30c-463a6615d6bb",
   "metadata": {
    "jp-MarkdownHeadingCollapsed": true
   },
   "source": [
    "## Building it out\n",
    "\n",
    "Ok, now it seems like we need two things right now.\n",
    "\n",
    "1) Something to take our time stepper (`euler_forward`) and our function (`dFdt`) and create a series of outputs.\n",
    "\n",
    "2) Something to automate and animate the plotting process.\n",
    "\n",
    "Let's start with 1).\n",
    "We'll make a basic function `run_simulation` to produce our values $\\theta$ and $v$ via our ODE and time stepper."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5272a49b-c608-4742-965b-4d6243cce011",
   "metadata": {},
   "outputs": [],
   "source": [
    "function run_simulation(x_0, h, dF, time_stepper, t_f)\n",
    "    # Setup\n",
    "    num_steps = Int(ceil(t_f / h))\n",
    "    x = zeros((num_steps + 1, maximum(size(x_0))))\n",
    "    x[1, :] = x_0\n",
    "\n",
    "    # Simulate\n",
    "    for i in 1:num_steps\n",
    "        x[i + 1, :] = time_stepper(x[i, :], h, dF)\n",
    "    end\n",
    "    x\n",
    "end\n",
    "\n",
    "# And run it\n",
    "h   = 1e-3\n",
    "t_f = 30 # s\n",
    "x  = run_simulation(x_0, h, dFdt, euler_forward, t_f)\n",
    "\n",
    "# Plotting the θ and v as a sanity check\n",
    "plot([x[:, 1], x[:, 2]], xlabel=\"\\$n\\$\", ylabel=\"\\$\\\\theta\\$\", label=[\"\\$\\\\theta\\$\" \"\\$v\\$\"])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "757e8cfb-8903-467b-8d77-2bed15c33660",
   "metadata": {},
   "source": [
    "Take a look at our plots of $\\theta$ and $v = d \\theta / d t$.\n",
    "\n",
    "Is anything concerning to you?\n",
    "\n",
    "What happens if we use backward Euler instead?\n",
    "\n",
    "What about a $\\theta$ method instead?"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e6804ba1-28f3-4f07-949b-b638016a10d8",
   "metadata": {},
   "source": [
    "## Animation\n",
    "\n",
    "The plotting isn't too bad once we have the solution array."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0ab1d7ab-598e-4a51-8c11-d000cc142853",
   "metadata": {},
   "outputs": [],
   "source": [
    "# We wrap the plotting in an annotated loop\n",
    "animation = @animate for i in 1:maximum(size(x))\n",
    "    θ_n = x[i, 1]\n",
    "    x_n =  l * sin(θ_n)\n",
    "    y_n = -l * cos(θ_n)\n",
    "    plot([(0, 0), (x_n, y_n)],\n",
    "        xlims=[-l, l], ylims=[-l, l],\n",
    "        marker=:circle,\n",
    "        legend=:none, aspect_ratio=:equal)\n",
    "end every 100;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "02342a68-8eff-4e55-a5c1-bd9a40a162ec",
   "metadata": {},
   "outputs": [],
   "source": [
    "gif(animation, \"pedulum.gif\", fps=30)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
